/*\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
 ClickAbilities 
  Version: 2.0
  Author: Incoherent Toast

This is a cooldown script which can also be used to activate certain items.
You can use this into your script to allow more advanced item usages such as
bardiche and hammer without having interruption issues. 
To do this it needs to be properly incorperated into your script otherwise it
will mostly function as a cooldown display

This can add utility to scripting and so the display is an optional feature.
To incorperate this into your script you need to use one of these functions:
- Run()
- RunNoDisplay()

Here's an example usage:
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

var CA = import UI/ClickAbilities
?CA.Run()
  ?foe.distance > 21
    ?summon.count = 0
      CA.SummonCinder()
    :
      equipL trisk
    equipR Compound
  :?item.GetCooldown("hammer")<=0 & item.CanActivate()
    CA.Hamm("Heavy Hammer -shiny")
  :?item.GetCooldown("bardiche")<=0 & foe.distance < 7 & item.CanActivate()
    CA.Bard("Bardiche")
  :
    equipL Sword
    equipR Hammer

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\*/

var BardicheDefault = "bardiche"
var HammerDefault   = "heavy hammer"

var LongDash = true
/* This setting will mean that the dash/bash
buttons may use qstaff to extend dash reach */

func SysUpdateRate()
  ?sys.isPC
    return 4
  :
    return 8

func SetDisplay(Boolean)
  DisplayOn = Boolean
  ?!CAinit
    CA_BasePanel.Recycle()
    CAinit = true

var UpdateRate = SysUpdateRate()
/* This changes how often the ability display updates. 
Can be tweaked with "ClickAbilities.UpdateRate = x" */

var DisplayOn = true
var CAinit = true
var GetCD = item.GetCooldown

var BashText
var BashCanvas
var BashButton

var DashText
var DashCanvas
var DashButton

var QstaffText
var QstaffCanvas
var QstaffButton

var MindText
var MindCanvas
var MindButton

var HammerText
var HammerCanvas
var HammerButton

var BardText
var BardCanvas
var BardButton

var BladeText
var BladeCanvas
var BladeButton

var MaskText
var MaskCanvas
var MaskButton

var WispText
var WispCanvas
var WispButton

var CindSumButton
var VoidSumButton

var ScriptToggleText
var ScriptToggleButton

var CA_BasePanel

var BladeDelay
var WispInit = true

var MinimiseButton
var MinimiseToggle = false
func CheckSys()
  ?sys.isPC
    return 0
  return -10
var MobileHotbarOffset = CheckSys()
var ShiftX = screen.w - 1 + MobileHotbarOffset
var ShiftY = 1
var ActivationTime = -1

var StyleInvisible = ui.AddStyle("
^###
^###
^###")

func CheckInterruptProtocol()
  return ActivationTime = -1

func CheckItem(Item)
  return GetCD(Item)<=0 & item.CanActivate()

func Qstaff()
  ?CheckItem("quarterstaff")
    equip quarterstaff
    activate R
    ?item.left
      equipL @item.left@
    equip @item.right@

func Mask()
  ?CheckItem("mask") & CheckInterruptProtocol()
    SetReequips()
    InterruptProtocol = OneTickProtocol
    ActivationTime = 0
    equipR mask
    activate R
func Mind()
  ?CheckItem("mind") & CheckInterruptProtocol()
    SetReequips()
    InterruptProtocol = OneTickProtocol
    ActivationTime = 0
    equipL mind
func Dash()
  ?GetCD("dash")<=0 & CheckInterruptProtocol()
    ?LongDash
      ?CheckItem("quarterstaff") & foe.distance < 22 & foe.distance > 17
        equip quarterstaff
        activate R
    SetReequips()
    InterruptProtocol = OneTickProtocol
    ActivationTime = 0
    equipR dash
func Bash()
  ?GetCD("bash")<=0 & CheckInterruptProtocol()
    ?LongDash
      ?CheckItem("quarterstaff") & foe.distance < 22 & foe.distance > 17
        equip quarterstaff
        activate R
    SetReequips()
    InterruptProtocol = OneTickProtocol
    ActivationTime = 0
    equipR bash

func Bard(input)
  ?BardButton = input
    ?CheckItem("bardiche") & CheckInterruptProtocol()
      UseR(BardicheDefault)
  :
    UseR(input)
func Hamm(input)
  ?HammerButton = input
    ?CheckItem("hammer") & CheckInterruptProtocol()
      UseR(HammerDefault)
  :
    UseR(input)
func Hammer(input)
  UseR(input)
func Bardiche(input)
  UseR(input)

func UseR(Weap)
  SetReequips()
  InterruptProtocol = SuperAttackRightProtocol
  equip @Weap@
  activate R
  ActivationTime = 0

func UseL(Weap)
  SetReequips()
  InterruptProtocol = SuperAttackLeftProtocol
  equipL @Weap@
  activate L
  ActivationTime = 0

func Blade()
  ?CheckItem("blade") & CheckInterruptProtocol()
    BladeDelay = 20
    InterruptProtocol = BladeProtocol
    ActivationTime = 0
    screen.next()

func SummonCinder()
  ?CheckItem("fire_talisman") & CheckInterruptProtocol()
    UseL("Fire Talisman")
func SummonWeaver()
  ?CheckItem("aether_talisman") & CheckInterruptProtocol()
    UseL("Aether Talisman")

func WispActivate()
  ?summon.GetName() = "Cinderwisp"
    activate Cinderwisp
  :
    activate Voidweaver

var LeftReequip
var RightReequip

func SetReequips()
  ?item.left
    LeftReequip = item.left
  :
    LeftReequip = 0

  ?item.right
    RightReequip = item.right
  :
    RightReequip = 0

func Reequip()
  ?LeftReequip
    equipL @LeftReequip@
  ?RightReequip
    equip @RightReequip@

func ScriptToggle()
  ?ToggleEnabled
    InterruptProtocol = DisabledProtocol
    ScriptToggleText.text = "Script: OFF"
  :
    InterruptProtocol = EnabledProtocol
    ScriptToggleText.text = "Script: ON"
  ToggleEnabled = !ToggleEnabled

func MinimiseRight()
  MinimiseToggle = !MinimiseToggle
  ?MinimiseToggle
    MinimiseButton.text = "[«"
    ShiftX = screen.w + 14
  :
    MinimiseButton.text = "[»"
    ShiftX = screen.w - 1 + MobileHotbarOffset
func MinimiseUp()
  MinimiseToggle = !MinimiseToggle
  ?MinimiseToggle
    MinimiseButton.text = "[▼]"
    MinimiseButton.y = 11
    ShiftY = -10
  :
    MinimiseButton.text = "[▲]"
    MinimiseButton.y = 10
    ShiftY = 1

func UIButton(UI,X,Y,Func)
  UI.x = X
  UI.y = Y
  UI.w = 13
  UI.h = 1
  UI.text = ""
  UI.style = StyleInvisible
  UI.dock = top_left
  UI.anchor = top_left
  UI.SetPressed(Func)
  CA_BasePanel.Add(UI)
  return UI

func UIText(UI,X,Y)
  UI.x = X
  UI.y = Y
  UI.w = 18
  UI.h = 1
  UI.dock = top_left
  UI.anchor = top_left
  CA_BasePanel.Add(UI)
  return UI

func UICanvas(UI,Y)
  UI.x = 1
  UI.y = Y
  UI.w = 13
  UI.h = 1
  UI.dock = top_left
  UI.anchor = top_left
  UI.blend = add
  CA_BasePanel.Add(UI)
  return UI

func CA_StartUp()
  CAinit = false
  CA_BasePanel = ui.AddPanel()
  CA_BasePanel.dock = top_left
  CA_BasePanel.anchor = top_right
  CA_BasePanel.x = ShiftX //screen.w - 1
  CA_BasePanel.y = 1
  CA_BasePanel.w = 15
  CA_BasePanel.h = 10

  ScriptToggleText   = UIText( ui.AddText("Script: ON"), 3,0)
  ScriptToggleButton = UIButton( ui.AddButton(), 1,0,ScriptToggle)

  BashText = UIText( ui.AddText(), 1,1)
  BashButton = UIButton( ui.AddButton(), 1,1,Bash)
  BashCanvas = UICanvas( ui.AddCanvas(), 1)

  DashText = UIText( ui.AddText(), 1,2)
  DashButton = UIButton( ui.AddButton(), 1,2,Dash)
  DashCanvas = UICanvas( ui.AddCanvas(), 2)

  QstaffText = UIText( ui.AddText(), 1,3)
  QstaffButton = UIButton( ui.AddButton(), 1,3,Qstaff)
  QstaffCanvas = UICanvas( ui.AddCanvas(), 3)

  MindText = UIText( ui.AddText(), 1,4)
  MindButton = UIButton( ui.AddButton(), 1,4,Mind)
  MindCanvas = UICanvas( ui.AddCanvas(), 4)

  HammerText = UIText( ui.AddText(), 1,5)
  HammerButton = UIButton( ui.AddButton(), 1,5,Hamm)
  HammerCanvas = UICanvas( ui.AddCanvas(), 5)

  BardText = UIText( ui.AddText(), 1,6)
  BardButton = UIButton( ui.AddButton(), 1,6,Bard)
  BardCanvas = UICanvas( ui.AddCanvas(), 6)

  BladeText = UIText( ui.AddText(), 1,7)
  BladeButton = UIButton( ui.AddButton(), 1,7,Blade)
  BladeCanvas = UICanvas( ui.AddCanvas(), 7)

  MaskText = UIText( ui.AddText(), 1,8)
  MaskButton = UIButton( ui.AddButton(), 1,8,Mask)
  MaskCanvas = UICanvas( ui.AddCanvas(), 8)

  CindSumButton = UIButton( ui.AddButton(), 1,9,SummonCinder)
  CindSumButton.w = 3
  CindSumButton.text = "φ"
  CindSumButton.style = 1
  VoidSumButton = UIButton( ui.AddButton(), 11,9,SummonWeaver)
  VoidSumButton.w = 3
  VoidSumButton.text = "*"
  VoidSumButton.style = 1

  ?sys.isPC
    MinimiseButton = UIButton( ui.AddButton(), -1,4,MinimiseRight)
    MinimiseButton.text = "[»"
    MinimiseButton.w = 2
    MinimiseButton.h = 3
  :
    MinimiseButton = UIButton( ui.AddButton(), 6,10,MinimiseUp)
    MinimiseButton.text = "[▲]"
    MinimiseButton.w = 3
    MinimiseButton.h = 1

func PercentCD(CD_Current,Max_CD)
  return math.Min( (100* (Max_CD - CD_Current) ) / Max_CD,100)

var QstaffMaxDuration = 0
func CheckQstaffMaxDuration()
  ?QstaffMaxDuration = 0
    var QsCD = GetCD("quarterstaff")
    ?QsCD > 0
      QstaffMaxDuration = QsCD + 1

func ReturnQstaffMaxDuration()
  ?QstaffMaxDuration
    return QstaffMaxDuration
  return 195


func Update_CD_Bar(CA_Text, CA_Canvas, CD_Current, Max_CD, String)
  var Percent = PercentCD(CD_Current, Max_CD)
  ?Percent < 10
    var Space = "   "
  :?Percent < 100
    var Space = "  "
  :
    var Space = " "

  CA_Text.text = " " + Percent + "%" + Space + String

  var R = math.RoundToInt((100 - Percent) * 2.56)
  var G = math.RoundToInt(Percent * 1.28)
  CA_Canvas.SetBG(color.FromRGB(R,G,0))


func WispSet()
  WispText = UIText( ui.AddText(), 1,9)
  WispCanvas = UICanvas( ui.AddCanvas(), 9)
  WispButton = UIButton( ui.AddButton(), 1,9,WispActivate)
  CA_BasePanel.h++
  CindSumButton.y++
  VoidSumButton.y++
  WispInit = false

func WispReset()
  WispText.Recycle()
  WispCanvas.Recycle()
  WispButton.Recycle()
  CA_BasePanel.h = 10
  CindSumButton.y = 9
  VoidSumButton.y = 9
  WispInit = true

func Updates()
  Update_CD_Bar(BashText, BashCanvas, GetCD("bash"), 270, "Bash")
  Update_CD_Bar(DashText, DashCanvas, GetCD("dash"), 45, "Dash")
  Update_CD_Bar(QstaffText, QstaffCanvas, GetCD("quarterstaff"), ReturnQstaffMaxDuration(), "Qstaff")
  Update_CD_Bar(MindText, MindCanvas, GetCD("mind"), 360, "Mind")
  Update_CD_Bar(HammerText, HammerCanvas, GetCD("hammer"), 660, "Hammer")
  Update_CD_Bar(BardText, BardCanvas, GetCD("bardiche"), 900, "Bard")
  Update_CD_Bar(BladeText, BladeCanvas, GetCD("blade"), 2400, "Blade")
  Update_CD_Bar(MaskText, MaskCanvas, GetCD("mask"), 2400, "Mask")

  ?summon.GetName() = "Cinderwisp"
    ?WispInit
      WispSet()
    Update_CD_Bar(WispText, WispCanvas, GetCD("cinderwisp"), 2700, "Cinder")
  :?summon.GetName() = "Voidweaver"
    ?WispInit
      WispSet()
    Update_CD_Bar(WispText, WispCanvas, GetCD("voidweaver"), 2700, "Void")
  :?!WispInit
    WispReset()


var SummonTarget = ""
var SavedTime = totaltime

func ResetProtocol()
  ?ToggleEnabled
    InterruptProtocol = EnabledProtocol
  :
    InterruptProtocol = DisabledProtocol
  ActivationTime = -1

func SuperAttackRightProtocol()
  ?item.right.state >= 3 & ActivationTime > 0
    Reequip()
    ResetProtocol()
    return true
  ActivationTime++
  return false
  
func SuperAttackLeftProtocol()
  ?item.left.state >= 3 & ActivationTime > 0
    Reequip()
    ResetProtocol()
    return true
  ActivationTime++
  return false

func OneTickProtocol()
  ?ActivationTime > 0
    Reequip()
    ResetProtocol()
    return true
  ActivationTime++
  return false

func BladeProtocol()
  BladeDelay --
  ?BladeDelay = 0
    equip blade
    activate R
    screen.ResetOffset()
    SetReequips()
    InterruptProtocol = OneTickProtocol
    ActivationTime = 1

func EnabledProtocol()
  return true
func DisabledProtocol()
  return false

var ToggleEnabled = true
var InterruptProtocol = EnabledProtocol

func CheckMinimised()
  ?ShiftX ! CA_BasePanel.x
    ?ShiftX < CA_BasePanel.x
      CA_BasePanel.x --
    :
      CA_BasePanel.x ++
  :?ShiftY ! CA_BasePanel.y
    ?ShiftY < CA_BasePanel.y
      CA_BasePanel.y --
    :
      CA_BasePanel.y ++

func Display(InitTime)
  ?DisplayOn
    ?CAinit | totaltime=InitTime
      CA_StartUp()
    CheckMinimised()
    CheckQstaffMaxDuration()
    ?time % UpdateRate = 0 & !MinimiseToggle
      Updates()

func Run()
  Display(1)
  ?InterruptProtocol()
    return true
  return false

func RunNoDisplay()
  ?InterruptProtocol()
    return true
  return false



////This segment will only get run if the player imports the script directly without assigning it a variable.

?totaltime > 1
  Display(2)
  InterruptProtocol()
