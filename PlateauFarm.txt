/*
Rocky Plateau idle script by Cynthia7979
Dependencies:
  └ Utils/StdLib
  └ Utils/Debug
    └ Utils/Colors

States:
    126 Dialog 1        127 Dialog 2
    117 Transformed     118 Transformed & Tangible
    ------------   Phase 1   --------------
    32  Idle            2 Perf
    33  Attack (fissure on 6th)
    ------------   Phase 2   --------------
    32 Idle             2 Perf
    33 Attack depending on element
*/

// Libraries
var Stdlib = import Utils/StdLib
var Eq = import Utils/Equipments

// Config variables
var doHarvest = false

// Global vars
var armorPercent
var prevPhase = 1
var prevState
var attacksCount = 0

// Functions

// Always run every frame
Stdlib.AAC()
?maxarmor ! 0
    armorPercent = armor / maxarmor
:
    armorPercent = -1
disable npcDialog

// Main logic
?foe.count = 0 | foe.distance > 23 | 
^foe = phase1 & (
    ^foe.state = 126 | foe.state = 127 | foe.state = 128 | 
    ^foe.state = 100 | foe.state = 101 | foe.state = 102 | 
    ^foe.state = 117 | foe.state = 124
^) | (foe.state = 107 & foe ! phase3) | foe.hp = 0 |
^ (foe = phase3 & foe.state = 116)
    // Not in combat
    ?hp < maxhp
        equipL ouroboros
    :
        equipL triskelion
    equipR star
    ?harvest.distance < 10 & doHarvest
        equip shovel
:
    ?armor >= 14 & item.GetCooldown("wand_aether") <= 0
        Stdlib.SmartEquip("left","calamity")
        activate L
    :?Stdlib.UseMask() = 0
        Stdlib.UseBlade()
    
    /* -=-=-=-=-=-=-=-=-=-=-  PHASE 1  -=-=-=-=-=-=-=-=-=-=-*/
    ?foe = phase1
        Stdlib.SmartEquip("left",Eq.MaxCross1)
        Stdlib.SmartEquip("right",Eq.MaxCross2)
        ?foe.state = 2
            ?foe.time > 60 
                Stdlib.SmartEquip("right",Eq.Shield)
            ?prevState = 33
                attacksCount++
        :?foe.state = 32
            Stdlib.SmartEquip("right",Eq.Shield)
        :?foe.state = 33
            ?foe.time < 10
                Stdlib.SmartEquip("right",Eq.Shield)
            ?attacksCount = 5
                ?foe.time > 10 & item.GetCooldown("mind") <= 0
                    // Dodge beam
                    equipR mind
                    attacksCount = -1  // Will be incremented after attack completes
        ?attacksCount > 5
            // May happen if mindstone fails to activate
            attacksCount = 0
    /* -=-=-=-=-=-=-=-=-=-=-  PHASE 2  -=-=-=-=-=-=-=-=-=-=-*/
    :?foe = phase2
        ?prevPhase = 1
            attacksCount = 0
        
        ?foe = ice
            Stdlib.SmartEquip("both","fire crossbow")
        :?foe = fire
            Stdlib.SmartEquip("both","aether staff")
        :?foe = poison
            Stdlib.SmartEquip("both","ice crossbow")
        :?foe = vigor
            Stdlib.SmartEquip("left","poison wand")
            Stdlib.SmartEquip("right",Eq.Shield)
        :?foe = aether
            Stdlib.SmartEquip("left","vigor wand")
            Stdlib.SmartEquip("right",Eq.Shield)
        
        ?((foe.state = 32 & foe.time > 120) | foe.state = 33) &
        ^(foe = ice | foe = fire)
            // Dodge the more nasty damage
            ?item.GetCooldown("mind") <= 0
                equipR mind
            :?item.GetCooldown("quarterstaff") <= 0 & foe.distance > 10
                equip quarterstaff
                activate R
            :
                ?!(item.left)
                    Stdlib.SmartEquip("left",Eq.MaxCross1)
                Stdlib.SmartEquip("right",Eq.Shield)
    /* -=-=-=-=-=-=-=-=-=-=-  PHASE 3  -=-=-=-=-=-=-=-=-=-=-*/
    :?foe = phase3
        // Pretty much same thing as phase 1? At least for 5*
        // Also weakness is everything
        ?foe.armor > 0
            ?foe.distance < 6 & item.GetCooldown("hammer") <= 0
                equip heavy
                activate R
            :
                Stdlib.SmartEquip("left","hammer")
                ?foe.distance > 12
                    Stdlib.SmartEquip("right","trisk")
                :?foe.distance >= 6
                    Stdlib.SmartEquip("right",Eq.Shield)
                :
                    Stdlib.SmartEquip("right","moon")
        :
            ?foe.distance > 6
                ?hp < maxhp - 10
                    Stdlib.SmartEquip("left","vigor wand")
                :
                    Stdlib.SmartEquip("left",Eq.MaxCross1)
                Stdlib.SmartEquip("right",Eq.Shield)
            :
                ?hp < maxhp - 3
                    Stdlib.SmartEquip("left","vigor sword")
                    Stdlib.SmartEquip("right","moon")
                :
                    Stdlib.SmartEquip("both","heavy")
        ?foe.state = 115 & foe.time >= 65  // big attack
            ?item.GetCooldown("mind") <= 0
                equipR mind
            :?item.GetCooldown("quarterstaff") <= 0
                equip quarterstaff
                activate R
    ?foe = phase1
        prevPhase = 1
    :?foe = phase2
        prevPhase = 2
    :?foe = phase3
        prevPhase = 3
    prevState = foe.state
